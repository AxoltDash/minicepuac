{
module Grammar where
import Lexer
import Data.Char
}

%name parse
%tokentype { Token }
%error { parseError }

%token 
      var        { TokenId $$ }
      double     { TokenNum $$ }
      "#t"      { TokenBool True }
      "#f"      { TokenBool False }
      ':'       { TokenColon }
      '('       { TokenLParen }
      ')'       { TokenRParen }
      '+'       { TokenPlus }
      '-'       { TokenMinus }
      '/'       { TokenSlash }
      '*'       { TokenMul }
      "&&"      { TokenAnd }
      "||"      { TokenOr }
      "not"     { TokenNot }
      "let"     { TokenLet }
      "boolean" { TokenBoolean }
      "number"  { TokenNumber }
      "lambda"  { TokenLambda }
      "->"      { TokenArrow }
      '{'       { TokenLCurly }
      '|'       { TokenPipe }
      '}'       { TokenRCurly }
      "=="      { TokenEqEq }
      "!="      { TokenNeq }
      '>'       { TokenGT }
      ">="      { TokenGE }
%%

ASA :  var { Id $1 }
     | double { ANum $1 }
     | "#t" { ABool True }
     | "#f" { ABool False }
     | '(' '+' ASA ASA ')' { Add $3 $4 }
     | '(' '-' ASA ASA ')' { Sub $3 $4 }
     | '(' '*' ASA ASA ')' { Mul $3 $4 }
     | '(' '/' ASA ASA ')' { Div $3 $4 }
     | '(' "&&" ASA ASA ')' { And $3 $4 }
     | '(' "||" ASA ASA ')' { Or $3 $4 }
     | '(' "not" ASA ')' { Not $3 }
     | '(' "lambda" ':' Arrow '(' var ')' ASA ')' { Lambda $4 $6 $8 }
     | '(' "let" '(' var ':' Type ASA ')' ASA ')' { Let ($4, (wrapIfPrimitive $6)) $7 $9 }
     | '(' ASA ASA ')' { App $2 $3 }

Type : '(' Type ')' { $2 }
    | PrimitiveType { $1 }
    | Arrow { $1 }
    | RefinementType { $1 }

PrimitiveType: "number" { Number }
    |          "boolean" { Boolean }  

Arrow: Type "->" Type { Arrow ( wrapIfPrimitive $1) (wrapIfPrimitive $3) }

RefinementType: '{' var ':' Type '|' Predicate '}' { Refinement $4 $6 }

Predicate: var "==" double { if $3 == 0 then Zero else parseError [] }
         | var "!=" double { if $3 == 0 then NonZero else parseError [] }
         | var '>' double { if $3 == 0 then NonZero else parseError [] }
         | var ">=" double { if $3 == 0 then MaybeZero else parseError [] }
{

parseError :: [Token] -> a
parseError a = error $ show a

wrapIfPrimitive :: Type -> Type
wrapIfPrimitive t = case t of
  Number -> Refinement Number MaybeZero
  Boolean -> Refinement Boolean MaybeZero
  _ -> t

data Type
  = Boolean
  | Number
  | Arrow Type Type
  | Refinement Type Predicate
  deriving (Show, Eq)

data Predicate 
  = NonZero
  | Zero 
  | MaybeZero
  deriving (Show, Eq)

data ASA
  = ANum Double
  | ABool Bool
  | Id String
  | Add ASA ASA
  | Sub ASA ASA
  | Mul ASA ASA
  | Div ASA ASA
  | And ASA ASA
  | Or ASA ASA
  | Not ASA
  | Lambda Type String ASA
  | App ASA ASA
  | Let (String, Type) ASA ASA
  deriving (Show, Eq)

main = getContents >>= print . parse . lexer

}
